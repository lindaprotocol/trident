def VERSION = '0.10.0'
group 'io.github.lindaprotocol'

buildscript {
    repositories {
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
        }
        mavenCentral()
    }
    dependencies {
        classpath "io.github.gradle-nexus:publish-plugin:1.1.0"
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'
apply plugin: "io.github.gradle-nexus.publish-plugin"
apply plugin: "signing"

ext {
    bouncycastleVersion = '1.78.1'
    junitJupiterVersion = '5.4.2'
    googleGuavaVersion = '33.0.0-jre'
}

allprojects {
    version = VERSION

    repositories {
        mavenCentral()
    }


    tasks.withType(Javadoc).tap {
        configureEach {
            options.encoding = 'UTF-8'
            options.charSet = 'UTF-8'
            options.addStringOption('Xdoclint:none', '-quiet')

            options {
                tags = [
                        'apiNote:a:API Note:',
                        'implSpec:a:Implementation Requirements:',
                        'implNote:a:Implementation Note:'
                ]
            }

            exclude '**/internal/**'
            exclude '**/impl/**'
            failOnError = false

            logging.captureStandardError LogLevel.INFO
            logging.captureStandardOutput LogLevel.INFO
        }
    }

    configurations.configureEach {
        resolutionStrategy {
            force "com.google.guava:guava:$googleGuavaVersion"
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'

    tasks.withType(Jar).configureEach {
        destinationDirectory = rootProject.file("build/libs")
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter:$junitJupiterVersion"

        compileOnly 'org.projectlombok:lombok:1.18.12'
        annotationProcessor 'org.projectlombok:lombok:1.18.12'
        testCompileOnly 'org.projectlombok:lombok:1.18.12'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
    }

    // tasks.register('sourcesJar', Jar) {
    //     dependsOn classes
    //     classifier = 'sources'
    //     from sourceSets.main.allSource
    // }

    // tasks.register('javadocJar', Jar) {
    //     dependsOn javadoc
    //     classifier = 'javadoc'
    //     from javadoc.destinationDir
    // }

    tasks.withType(JavaCompile).configureEach {
        //options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    configurations {
        checkstyleConfig
    }
    configurations.named('checkstyleConfig') {
        transitive = false
    }
    checkstyle {
        toolVersion = "8.29"
        configFile = file("../config/checkstyle/checkStyleAll.xml")
    }
    checkstyleMain {
        source = 'src/main/java'
        exclude '**/trident/api/**'
        exclude '**/trident/proto/**'
    }
    checkstyleTest {
        source = files() //use empty files to ignore test class
    }
   }

shadowJar {
    zip64 = true
}

javadoc {
    source subprojects.collect { project ->
        project.sourceSets.main.allJava
    }
    classpath = files(subprojects.collect { project ->
        project.sourceSets.main.compileClasspath
    })
}

tasks.register('javadocJar', Jar) {
    archiveClassifier = 'javadoc'
    archiveVersion = VERSION
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    archiveVersion = VERSION
    from subprojects.collect { project ->
        project.sourceSets.main.allSource
    }
}

tasks.register('buildLib', Jar) {
    archiveVersion = VERSION
    from subprojects.collect { project ->
        project.sourceSets.main.output
    }
}

tasks.named('build') {
    dependsOn javadocJar, sourcesJar, buildLib
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        trident(MavenPublication) {
            from components.java

            artifact tasks.sourcesJar
            artifact tasks.javadocJar

            pom {
                name = 'Trident'
                description = 'Linda blockchain Java SDK with comprehensive API support'
                url = 'https://github.com/lindaprotocol/trident'
                
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                        distribution = 'repo'
                    }
                }
              
                developers {
                    developer {
                        name = "LINDA Foundation Development Team"
                        email = "lindaprotocol@linda.network"
                        organization = "LINDA Foundation"
                        organizationUrl = "https://linda.network/"
                    }
                }
            
                scm {
                    connection = 'scm:git:git://github.com/lindaprotocol/trident.git'
                    developerConnection = 'scm:git:ssh://github.com:lindaprotocol/trident.git'
                    url = 'https://github.com/lindaprotocol/trident/tree/main'
                }
            }
        }
    }
}


// Configure Nexus publishing
nexusPublishing {
    repositories {
        sonatype {
            packageGroup = "io.github.lindaprotocol"
            stagingProfileId = "io.github.lindaprotocol"
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/content/repositories/snapshots/"))
            
            username = findProperty("sonatypeUsername") ?: System.getenv("SONATYPE_USERNAME")
            password = findProperty("sonatypePassword") ?: System.getenv("SONATYPE_PASSWORD")
        }
    }
}

// Signing configuration for Maven Central
signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")

    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.trident
    } else {
        def keyId = findProperty("signing.keyId")
        def password = findProperty("signing.password")
        def secretKeyRingFile = findProperty("signing.secretKeyRingFile")

        if (keyId && password && secretKeyRingFile) {
            sign publishing.publications.trident
        } else {
            println "Warning: Signing disabled. No signing credentials found."
        }
    }
}
